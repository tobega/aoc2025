composer parse-intervals
  [<interval>+]
  rule interval: { start: <INT "1">, (<='-'>) end: <INT "1"> } (<=','>?)
end parse-intervals

def intervals: $IN::readline -> parse-intervals;

processor InvalidIdGenerator
  processor Repeat
    def top: $(top:);
    def base: $(value:);
    @: $(value:);

    source multiple
      @Repeat: ($@Repeat * $top + $base) * $top + $base;
      $@Repeat !
    end multiple
  end Repeat

  @: {top: 10"1", value: 0"1", half-multiplier: 10"1", half: 1"1"};
  source next
    $@InvalidIdGenerator(value:) + 1"1" -> #
    $@InvalidIdGenerator -> $.value * $.top + $.value !

    when <$@InvalidIdGenerator(top:)..> do
      @InvalidIdGenerator: $@InvalidIdGenerator(
        { top: ยง(top:) * 10"1",
          value: $,
          half-multiplier: (ยง(half-multiplier:) * 10"1") mod 99"1",
          half: ยง(half:) * ยง(half-multiplier:)
        });
    otherwise
      @InvalidIdGenerator(value:): $;
 end next

 source pattern-repeater
   // Assumption: only need to exclude values that are a double-pattern
   $@InvalidIdGenerator -> #
   
   when <{half-multiplier: <=10"1">}
     |{half-multiplier: <=1"1">}?($(value:) ~/ $(half:) <~=$(value:) mod $(half:)>)> do
     $@InvalidIdGenerator -> Repeat !
  end pattern-repeater
end InvalidIdGenerator

processor Accumulator
  def ranges: $;
  @: 0"1";

  templates add-in-range
    def value: $;
    @: { add: 0"1", remaining:[] };
    $ranges... -> #
    @Accumulator: $@Accumulator + $@(add:);
    $@(remaining:)... !

    when <{start: <..$value>, end: <$value..>}> do
      @: { add: $@(add:) + $value, remaining: [1] };
    when <{end: <$value~..>}> do
      @(remaining:): [1];
  end add-in-range

  source result
    $@Accumulator !
  end result
end Accumulator

templates part1
  def accumulator: $ -> Accumulator;
  def generator: $InvalidIdGenerator;
  $generator::next -> #
  $accumulator::result !

  otherwise
    $ -> accumulator::add-in-range -> $generator::next -> #
end part1

$intervals -> part1 -> !OUT::write
'
' -> !OUT::write

templates part2
  def accumulator: $ -> Accumulator;
  def generator: $InvalidIdGenerator;
  $generator::next -> #
  $accumulator::result !

  otherwise
    $generator::pattern-repeater -> \(
        def pattern-repeater: $;
        $pattern-repeater::multiple -> #

        otherwise
          $ -> accumulator::add-in-range -> $pattern-repeater::multiple -> #
    \) -> !VOID
    $ -> accumulator::add-in-range -> $generator::next -> #
end part2

$intervals -> part2 -> !OUT::write
'
' -> !OUT::write

test 'part 2'
  assert '11-22,95-115,998-1012,1188511880-1188511890,222220-222224,1698522-1698528,446443-446449,38593856-38593862,565653-565659,824824821-824824827,2121212118-2121212124'
  -> parse-intervals -> part2 <=4174379265"1"> 'example'
end 'part 2'
