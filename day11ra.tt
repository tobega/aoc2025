composer parse-output-list
  (def from: <node>; <=':'>) <node>+ -> {from: $from, to: $}
  rule node: (<WS>?) <node´'[a-z]{3}'>
end parse-output-list

def connections: {| $IN::lines -> parse-output-list |};

// find the fix-point
source find-fix&{from:}
  @: {||};
  {|{to: $from, paths: 1"1"}|} -> #
  $@ !
  when <~=$@> do
    @: $;
    ($({paths:, from: §(to:)}) join $connections) -> $(collect {paths: Sum&{of: :(paths:)}} by $({to:}))
    -> ($ union $@) -> $(collect {paths: Max&{by: :(paths:), select: :(paths:)}} by $({to:})) -> #
end find-fix

templates get-count&{to:}
  @: 0"1";
  $ -> ($ matching {|{to: $to}|}) ... -> $(paths:) -> @: ($)"1";
  $@!
end get-count

source part1
  $find-fix&{from: 'you'} -> get-count&{to: 'out'} !
end part1

$part1 -> !OUT::write
'
' -> !OUT::write

source part2
  def from-svr: $find-fix&{from: 'svr'};
  def from-fft: $find-fix&{from: 'fft'};
  def from-dac: $find-fix&{from: 'dac'};
  ($from-svr -> get-count&{to:'fft'})"1" * ($from-fft -> get-count&{to: 'dac'})"1" * ($from-dac -> get-count&{to: 'out'})"1"
  + ($from-svr -> get-count&{to: 'dac'})"1" * ($from-dac -> get-count&{to: 'fft'})"1" * ($from-fft -> get-count&{to: 'out'})"1"
  !
end part2

$part2 -> !OUT::write
'
' -> !OUT::write
